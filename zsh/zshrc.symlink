#enable all the extended expansion/globbing feature
setopt extended_glob

# shortcut to this dotfiles path is $ZSH_CFG
export ZSH_CFG=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/Code

# Set up antigen path
export ANTIGEN=$HOME/.antigen

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files but antigen
typeset -U config_files
config_files=($ZSH_CFG/(^antigen/)#*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files


# Load antigen and bootstrap the configuration
if [[ -f $ANTIGEN/antigen.zsh ]]; then
    source $ANTIGEN/antigen.zsh
else
    git clone --recursive https://github.com/zsh-users/antigen $ANTIGEN
    source $ANTIGEN/antigen.zsh
fi

if [[ "$_ANTIGEN_INIT_ENABLED" == "true" ]]; then
  antigen init ~/.antigenrc
else
  source ~/.antigenrc
fi

